apply plugin: 'com.android.application' //应用Android Gradle 插件
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'
apply from: 'versionManager.gradle'
android {
    compileSdkVersion 30  //编译所依赖的Android SDK版本
    buildToolsVersion "29.0.3" //构建Android工程所用的构建工具的版本

    //ProductFlavor
    defaultConfig {
        applicationId "com.baima.jetpack"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode appVersionCode
        versionName appVersionCodeName

        testApplicationId "com.baima.jetpack.test"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testHandleProfiling true //是否启用性能分析
        testFunctionalTest true //是否启用功能测试
    }

    testBuildType 'debug'


    flavorDimensions "abi","version"
    productFlavors{
        baidu{
            dimension "abi"
            manifestPlaceholders.put("ZW","baidu")
            buildConfigField 'String','url','"baiduurl"'
            resValue 'string','channel_tip','百度渠道'
            resValue 'bool','channelB','true'
        }
        google{
            dimension "abi"
            manifestPlaceholders.put("ZW","google")
            buildConfigField 'String','url','"googleUrl"'
            resValue 'string','channel_tip','谷歌渠道'
            resValue 'bool','channelB','false'
        }

        paid{
            dimension "version"
        }
    }


    //相当于SourceSet
    buildTypes {
        release {
            buildConfigField 'String','name','"release"'
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
           // signingConfig signingConfigs.release
        }
        stag{
            buildConfigField 'String','name','"stag"'
        }

        debug{
            buildConfigField 'String','name','"debug"'
            minifyEnabled false
         //   signingConfig signingConfigs.debug
        }
    }

    compileOptions {
        encoding 'UTF-8'
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    // For Kotlin projects
    kotlinOptions {
        jvmTarget = "1.8"
    }

    adbOptions{
        timeOutInMs 5*1000
        installOptions '-r','-s'
    }
    dexOptions{
        javaMaxHeapSize '4g' //执行dx命令为其分配的最大内存
        jumboMode true //函数超过了65535，需要强制开启jumbo模式才能构建成功，默认false
        preDexLibraries true //是否预执行dex Libraries库工程，默认为true，提高增量构建的速度
        threadCount 2 //运行dx命令使用的线程数

    }


}

dependencies {
    testImplementation 'junit:junit:4.12'

    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    implementation "com.google.dagger:hilt-android:2.28-alpha"
    kapt "com.google.dagger:hilt-android-compiler:2.28-alpha"
    implementation 'androidx.viewpager2:viewpager2:1.1.0-alpha01'
    implementation 'androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha02'
    kapt 'androidx.hilt:hilt-compiler:1.0.0-alpha02'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    implementation 'io.reactivex:rxjava:1.3.8'
    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'androidx.appcompat:appcompat:1.2.0'
}

task printVersion(){
    println("print")
}

